@echo off

echo ##########################################################################
echo ParaView %VERSION% %PYTHON_VERSION% %PYTHON_VERSION:.=%
echo ##########################################################################

IF NOT DEFINED SAT_DEBUG (
  SET SAT_DEBUG=0
)

IF NOT DEFINED CMAKE_GENERATOR (
  SET CMAKE_GENERATOR="Visual Studio 15 2017 Win64"
)

if NOT exist "%PRODUCT_INSTALL%" mkdir %PRODUCT_INSTALL%
REM clean BUILD directory
if exist "%BUILD_DIR%" rmdir /Q /S %BUILD_DIR%
mkdir %BUILD_DIR%

SET PRODUCT_BUILD_TYPE=Release
REM TODO: NGH: not Tested yet
REM if %SAT_DEBUG% == 1 (
REM   set PRODUCT_BUILD_TYPE=Debug
REM )

set CMAKE_OPTIONS=

set PVLIBVERSION=5.6

set python_name=python%PYTHON_VERSION%

REM verbose log
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCMAKE_VERBOSE_MAKEFILE=ON

REM common settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCMAKE_INSTALL_PREFIX:STRING=%PRODUCT_INSTALL:\=/%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCMAKE_BUILD_TYPE:STRING=%PRODUCT_BUILD_TYPE%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBUILD_SHARED_LIBS:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBUILD_TESTING:BOOL=OFF

REM Paraview general settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_INSTALL_DEVELOPMENT_FILES:BOOL=ON

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_USE_MPI:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_ENABLE_CATALYST:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_ENABLE_COPROCESSING:BOOL=OFF


REM VTK general settings
REM # issue 1779
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_64BIT_IDS:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_PYTHON_FULL_THREADSAFE=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_NO_PYTHON_THREADS:BOOL=OFF  

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_PYGMENTS:BOOL=ON

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_INSTALL_LIBRARY_DIR=lib/paraview-%PVLIBVERSION%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_INSTALL_ARCHIVE_DIR=lib/paraview-%PVLIBVERSION%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_PYTHON_SITE_PACKAGES_SUFFIX=site-packages
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_INSTALL_PLUGINS_DIR=lib/paraview-%PVLIBVERSION%/plugins
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTKm_INSTALL_LIB_DIR=lib/paraview-%PVLIBVERSION%

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_REPORT_OPENGL_ERRORS:BOOL=OFF

REM OpenGL2 backend for performance improvment
REM https://blog.kitware.com/new-opengl-rendering-in-vtk/ 
REM OpenGl2 mandatory in this version of paraview
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_RENDERING_BACKEND:STRING=OpenGL2

REM OpenMP to speed computation of some filters
REM https://blog.kitware.com/simple-parallel-computing-with-vtksmptools-2/
REM https://blog.kitware.com/accelerated-filters-in-paraview-5/
REM set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_SMP_IMPLEMENTATION_TYPE=OpenMP

REM Qt settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_QT_GUI:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_QT_VERSION=5
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DQT_HELP_GENERATOR:STRING=%QT5_ROOT_DIR:\=/%/bin/qhelpgenerator.exe

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_BUILD_QT_DESIGNER_PLUGIN:BOOL=OFF
REM # additionaly for windows
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DQT_QMAKE_EXECUTABLE:STRING=%QT5_ROOT_DIR:\=/%/bin/qmake.exe
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DQT_BINARY_DIR:STRING=%QT5_ROOT_DIR:\=/%/bin/

REM # Python settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_ENABLE_PYTHON:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_WRAP_PYTHON:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPYTHON_EXECUTABLE:STRING=%PYTHON_ROOT_DIR:\=/%/python.exe
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPYTHON_INCLUDE_DIR:STRING=%PYTHON_ROOT_DIR:\=/%/include
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPYTHON_LIBRARY:STRING=%PYTHON_ROOT_DIR:\=/%/libs/python%PYTHON_VERSION:.=%.lib
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_WINDOWS_PYTHON_DEBUGGABLE:BOOL=OFF
REM No tcl tk wrap
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_WRAP_JAVA:BOOL=OFF

REM HDF5 settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_HDF5:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DHDF5_INCLUDE_DIRS:STRING=%HDF5_ROOT_DIR:\=/%/include

set LIST_HDF5_LIBRARIES=%HDF5_ROOT_DIR:\=/%/lib/hdf5_cpp.lib
set LIST_HDF5_LIBRARIES=%LIST_HDF5_LIBRARIES%;%HDF5_ROOT_DIR:\=/%/lib/hdf5_hl_cpp.lib
set LIST_HDF5_LIBRARIES=%LIST_HDF5_LIBRARIES%;%HDF5_ROOT_DIR:\=/%/lib/hdf5.lib
set LIST_HDF5_LIBRARIES=%LIST_HDF5_LIBRARIES%;%HDF5_ROOT_DIR:\=/%/lib/hdf5_hl.lib
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DHDF5_LIBRARIES:STRING=%LIST_HDF5_LIBRARIES%

REM VisIt Database bridge settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_USE_VISITBRIDGE=ON

REM Boost settings (needed when activating VisIt bridge)
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBOOST_ROOT=%BOOST_ROOT_DIR:\=/%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBOOST_ROOT:PATH=%BOOST_ROOT_DIR:\=/%
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBoost_ADDITIONAL_VERSIONS="1.67.0 1.67"
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBOOST_INCLUDEDIR=%BOOST_ROOT_DIR:\=/%/include/boost-1_67
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DBoost_INCLUDE_DIR=%BOOST_ROOT_DIR:\=/%/include/boost-1_67
REM gl2ps settings
REM VTK and system gl2ps conflict resolution
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_GL2PS:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DGL2PS_INCLUDE_DIR:STRING=%GL2PS_ROOT_DIR:\=/%/include
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DGL2PS_LIBRARY:STRING=%GL2PS_ROOT_DIR:\=/%/lib/gl2ps.lib

REM # libxml2 settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_LIBXML2:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DLIBXML2_INCLUDE_DIR:STRING=%LIBXML2_ROOT_DIR:\=/%/include
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DLIBXML2_LIBRARIES:STRING=%LIBXML2_ROOT_DIR:\=/%/lib/libxml2.lib
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DLIBXML2_XMLLINT_EXECUTABLE=%LIBXML2_ROOT_DIR:\=/%/bin/xmllint.exe

REM # freetype settings
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_FREETYPE:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DFREETYPE_INCLUDE_DIR_freetype2:STRING=%FREETYPE_ROOT_DIR:\=/%/include/freetype2
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DFREETYPE_LIBRARY:STRING=%FREETYPE_ROOT_DIR:\=/%/lib/freetype.lib

REM Extra options (switch off non-used Paraview plug-ins)
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_Moments:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_SLACTools:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_SierraPlotTools:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_PacMan:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_MobileRemoteControl:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_VTKmFilters:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_ENABLE_COPROCESSING:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_CATALYST_ADAPTORS:BOOL=OFF
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_ENABLE_WEB=OFF

REM Extra options (switch on required Paraview plug-ins)
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_EyeDomeLighting:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_ForceTime:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_H5PartReader:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_PointSprite:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_BUILD_PLUGIN_SurfaceLIC:BOOL=ON

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCGNS_FIND_REQUIRED:BOOL=ON
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCGNS_INCLUDE_DIR:STRING=%CGNS_ROOT_DIR:\=/%/include
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCGNS_LIBRARY:STRING=%CGNS_ROOT_DIR:\=/%/lib/cgns.lib
set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DVTK_USE_SYSTEM_CGNS:BOOL=ON

if defined OSPRAY_ROOT_DIR (
   set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DPARAVIEW_USE_OSPRAY:BOOL=ON -DOSPRAY_INSTALL_DIR:STRING=%OSPRAY_ROOT_DIR:\=/%
)

set CMAKE_OPTIONS=%CMAKE_OPTIONS% -DCMAKE_GENERATOR=%CMAKE_GENERATOR%

cd %BUILD_DIR%
echo.
echo INFO: running command: %CMAKE_ROOT%\bin\cmake %CMAKE_OPTIONS% %SOURCE_DIR%

%CMAKE_ROOT%\bin\cmake %CMAKE_OPTIONS% %SOURCE_DIR%
if NOT %ERRORLEVEL% == 0 (
    echo "ERROR on cmake"
    exit 1
)

echo.
echo --------------------------------------------------------------------------
echo *** msbuild %MAKE_OPTIONS% ALL_BUILD.vcxproj /p:Configuration=%PRODUCT_BUILD_TYPE% /p:Platform=x64
echo --------------------------------------------------------------------------

msbuild %MAKE_OPTIONS% ALL_BUILD.vcxproj /p:Configuration=%PRODUCT_BUILD_TYPE% /p:Platform=x64
if NOT %ERRORLEVEL% == 0 (
    echo ERROR on msbuild ALL_BUILD.vcxproj
    exit 2
)

echo.
echo --------------------------------------------------------------------------
echo *** msbuild %MAKE_OPTIONS% INSTALL.vcxproj /p:Configuration=%PRODUCT_BUILD_TYPE% /p:Platform=x64
echo --------------------------------------------------------------------------

msbuild %MAKE_OPTIONS% INSTALL.vcxproj /p:Configuration=%PRODUCT_BUILD_TYPE% /p:Platform=x64
if NOT %ERRORLEVEL% == 0 (
    echo ERROR on msbuild INSTALL.vcxproj
    exit 3
)

if defined OSPRAY_ROOT_DIR (
    sed -i "s/;ospray::ospray//g" %PRODUCT_INSTALL:\=/%/lib/cmake/paraview-%PVLIBVERSION%/ParaViewTargets.cmake
    sed -i "s/;ospray::ospray//g" %PRODUCT_INSTALL:\=/%/lib/cmake/qttesting/ParaViewTargets.cmake
)

REM in order to fullfill some prerequistes by GUI!
cd %PRODUCT_INSTALL%\bin
mkdir Lib
MOVE /Y site-packages Lib\site-packages

REM move 
set MSBUILDDISABLENODEREUSE=1

echo.
echo --------------------------------------------------------------------------
echo *** Post processing
echo --------------------------------------------------------------------------
echo.

echo
echo "########## END"

